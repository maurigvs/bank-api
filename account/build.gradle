plugins {
	id "java"
	id "org.springframework.boot" version "3.2.3"
	id "io.spring.dependency-management" version "1.1.4"
	id "com.google.protobuf" version "0.9.4"
}

group = "com.maurigvs.bank"
version = "0.0.1-SNAPSHOT"

java {
	sourceCompatibility = "17"
}

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

def grpcVersion = "1.59.0"
def protobufVersion = "3.24.0"
def protocVersion = protobufVersion
def loggingVersion = "2.23.0"

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-services:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	implementation "org.apache.logging.log4j:log4j-core:${loggingVersion}"
	implementation "org.apache.logging.log4j:log4j-api:${loggingVersion}"

	compileOnly "org.apache.tomcat:annotations-api:6.0.53"

	runtimeOnly "com.h2database:h2"
	runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}

sourceSets {
	main {
		java {
			srcDirs "build/generated/source/proto/main/grpc"
			srcDirs "build/generated/source/proto/main/java"
		}
	}
}

tasks.named("test") {
	useJUnitPlatform()
}
